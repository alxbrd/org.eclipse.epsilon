gunit Flock;
@header{package org.eclipse.epsilon.flock.parse;}

flockModule:
	// Empty, single rule, and many rules.
	"" -> "FLOCKMODULE"
	"migrate Person { name := nom; }" -> "(FLOCKMODULE (MIGRATE Person (BLOCK (:= name nom))))"
	"migrate Person { name := nom; } migrate Animal { name := nom; }" -> "(FLOCKMODULE (MIGRATE Person (BLOCK (:= name nom))) (MIGRATE Animal (BLOCK (:= name nom))))"

	// Operations
	"operation Integer inc() { self := self + 1; }" -> "(FLOCKMODULE (HELPERMETHOD Integer inc (BLOCK (:= self (+ self 1)))))"
	"@cached operation Integer inc() { self := self + 1; }" -> "(FLOCKMODULE (ANNOTATIONBLOCK @cached operation Integer inc() { self := self + 1; }))"

fullMigrateRule:
	// Empty
	"migrate Person { }" -> "(MIGRATE Person BLOCK)"
	
	// Missing brace
	"migrate Person { name := nom; " FAIL
	
	// Statement
	"migrate Person { name := nom; }" -> "(MIGRATE Person (BLOCK (:= name nom)))"
	
	// To part
	"migrate Animal to Dog { }" -> "(MIGRATE Animal Dog BLOCK)"
	"migrate Animal to { }" FAIL
		
	// Guard
	"migrate Person when: original.name.isDefined() { name := nom; }" -> "(MIGRATE Person (GUARD (. (. original name) (isDefined PARAMETERS))) (BLOCK (:= name nom)))"

	// Guard with no statement, no when
	"migrate Person when {}" FAIL
	"migrate Person original.name.isDefined(); {}" FAIL
	
	// Guard with semicolon
 	"migrate Person when: original.name.isDefined(); {}" FAIL

	// No body
	"migrate Person" FAIL


shorthandMigrateRule:
	"migrate Person to Salesperson" -> "(MIGRATE Person Salesperson)"
	
	// No to part
	"migrate Person" FAIL


guard:
	"when: original.name.isDefined()" -> "(GUARD (. (. original name) (isDefined PARAMETERS)))"
	"when { var n := original.name; return n.isDefined(); }" -> "(GUARD (BLOCK (:= (var n) (. original name)) (RETURN (. n (isDefined PARAMETERS)))))"
		
	// Guard with no when
	"original.name.isDefined()" FAIL
 
 deleteRule:
 	// Typical
 	"delete Person"	-> (DELETE Person)
 	
 	// with guard
 	"delete Person when: original.name.isUndefined()" -> "(DELETE Person (GUARD (. (. original name) (isUndefined PARAMETERS))))"