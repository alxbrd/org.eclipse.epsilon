@data elems
operation data() {
  return Sequence {
    Sequence { 'http://www.eclipse.org/emf/2002/Ecore', 'ecore' },
    Sequence { 'http://www.eclipse.org/emf/2002/GenModel', 'genmodel' },
    Sequence { 'http://www.eclipse.org/gmf/2006/GraphicalDefinition', 'gmfgraph' },
    Sequence { 'http://www.eclipse.org/gmf/2005/ToolDefinition', 'gmftool' },
    Sequence { 'http://www.eclipse.org/gmf/2008/mappings', 'gmfmap' },
    Sequence { 'http://www.eclipse.org/gmf/2009/GenModel', 'gmfgen' }
  };
}

@model
operation loadModels() {
  loadModel('Expected', direxpected);
  loadModel('Obtained', dirtemp);
}

@test
operation testForRegressions() {
  var modelExtension := elems.get(1);

  -- Temporary workaround: we need to ignore some elements
  -- which produce differences even with sets of files that
  -- are exactly the same: the metafeatures in the GmfMap
  -- and GmfGen models.
  if (modelExtension == 'gmfmap') {
    for (featureMap in Expected!FeatureLabelMapping) {
      featureMap.features.clear();
    }
    for (featureMap in Obtained!FeatureLabelMapping) {
      featureMap.features.clear();
    }
  }
  else if (modelExtension == 'gmfgen') {
    for (featLabel in Expected!FeatureLabelModelFacet) {
      featLabel.metaFeatures.clear();
    }
    for (featLabel in Obtained!FeatureLabelModelFacet) {
      featLabel.metaFeatures.clear();
    }
  }

  assertEqualModels('Expected', 'Obtained');
}

operation loadModel(modelName, directory) {
  var metamodelUri   := elems.get(0);
  var modelExtension := elems.get(1);

  var task := antProject.createTask('epsilon.emf.loadModel');
  task.setName(modelName);
  task.setModelFile(new Native('java.io.File')(
    directory + '/' + casename + '.' + modelExtension));
  task.setMetamodelUri(metamodelUri);
  task.setRead(true);
  task.setStore(false);
  task.execute();
}
